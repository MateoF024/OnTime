
plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
}

def minecraftVer = project.minecraft_version
def versionType

if (minecraftVer.startsWith("1.20.1")) {
    versionType = "v1.20.1"
} else if (minecraftVer.startsWith("1.21.1") || minecraftVer.startsWith("1.21.2") || minecraftVer.startsWith("1.21.3") || minecraftVer.startsWith("1.21.4") || minecraftVer.startsWith("1.21.5")) {
    versionType = "v1.21.1"
} else if (minecraftVer.startsWith("1.21.6") || minecraftVer.startsWith("1.21.7") || minecraftVer.startsWith("1.21.8") || minecraftVer.startsWith("1.21.9") || minecraftVer.startsWith("1.21.10")) {
    versionType = "v1.21.6"
} else {
    versionType = "v1.21.1"
}

base {
    archivesName = "${project.archives_base_name}+fabric+${project.minecraft_version}"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        ontime {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    '../common/src/main/java',
                    "src/${versionType}/main/java"
            ]
        }
        resources {
            srcDirs = [
                    'src/main/resources',
                    '../common/src/main/resources',
                    "src/${versionType}/main/resources"
            ]
        }
    }

    client {
        java {
            srcDirs = [
                    'src/client/java',
                    "src/${versionType}/client/java"
            ]
        }
        resources {
            srcDirs = [
                    'src/client/resources',
                    "src/${versionType}/client/resources"
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    implementation project(path: ':common', configuration: 'namedElements')
    include project(':common')
}

processResources {
    inputs.property "version", mod_version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

java {
    withSourcesJar()
}