plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.139'
}

def minecraftVer = project.minecraft_version
def versionType

if (minecraftVer.startsWith("1.21.1") || minecraftVer.startsWith("1.21.2") ||
        minecraftVer.startsWith("1.21.3") || minecraftVer.startsWith("1.21.4") ||
        minecraftVer.startsWith("1.21.5")) {
    versionType = "v1.21.1"
} else if (minecraftVer.startsWith("1.21.6") || minecraftVer.startsWith("1.21.7") ||
        minecraftVer.startsWith("1.21.8") || minecraftVer.startsWith("1.21.9") ||
        minecraftVer.startsWith("1.21.10")) {
    versionType = "v1.21.6"
} else {
    versionType = "v1.21.1"
}

base {
    archivesName = "${project.archives_base_name}+neoforge+${project.minecraft_version}"
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.neoforged.net/releases'
    }
}

neoForge {
    version = project.neoforge_version
    validateAccessTransformers = true

    runs {
        client {
            client()
        }

        server {
            server()
            programArgument '--nogui'
        }
    }

    mods {
        ontime {
            sourceSet sourceSets.main
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    '../common/src/main/java',
                    'src/client/java',
                    "src/${versionType}/java"
            ]
        }
        resources {
            srcDirs = [
                    'src/main/resources',
                    'src/client/resources',
                    '../common/src/main/resources'
            ]
        }
    }
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:2.0.9'
    implementation("me.shedaniel.cloth:cloth-config-neoforge:${project.cloth_version}")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title': project.mod_name,
                'Specification-Vendor': 'MateoF24',
                'Specification-Version': '1',
                'Implementation-Title': project.mod_name,
                'Implementation-Version': project.mod_version,
                'Implementation-Vendor': 'MateoF24'
        ])
    }
}